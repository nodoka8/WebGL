<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8">
    <title>myhomepage</title>
    <link rel="stylesheet" href="stylesheet.css">
    <script src="bgscript.js" type="text/javascript"></script>
    <script src="script.js" type="text/javascript"></script>
    <script src="minMatrix.js" type="text/javascript"></script>
    <script src="minMatrixb.js" type="text/javascript"></script>
    <script id="vs" type="x-shader/x-vertex">
      attribute vec3 position;

      void main(void){
      	gl_Position = vec4(position, 1.0);
      }
    </script>

    <script src="Primitives.js" type="text/javascript"></script>

    <script id="fs" type="x-shader/x-fragment">


      precision mediump float;

      uniform float time;
      uniform vec2  mouse;
      uniform vec2  resolution;

      const float PI = 3.14159265;
      const float angle = 60.0;
      const float fov = angle * 0.5 * PI / 180.0;
      const float sphereSize = 0.5;
      const vec3 lightDir = vec3(0.0, 1.0, 0.0);
      const vec3 cPos = vec3(0.0, 1.0, 2.0);
      const vec3 cDir = vec3(0.0, -0.707, -0.707);
      const vec3 cUp  = vec3(0.0,  0.707, -0.707);

      // rotate
      vec3 rotate(vec3 p, float angle, vec3 axis){
          vec3 a = normalize(axis);
          float s = sin(angle);
          float c = cos(angle);
          float r = 1.0 - c;
          mat3 m = mat3(
              a.x * a.x * r + c,
              a.y * a.x * r + a.z * s,
              a.z * a.x * r - a.y * s,
              a.x * a.y * r - a.z * s,
              a.y * a.y * r + c,
              a.z * a.y * r + a.x * s,
              a.x * a.z * r + a.y * s,
              a.y * a.z * r - a.x * s,
              a.z * a.z * r + c
          );
          return m * p;
      }

      vec3 trans(vec3 p){
          return mod(p, 4.0) - 2.0;
      }

      float sdSphere( vec3 p, float s )
      {
          return length(p)-s;
      }

      float sdHexPrism( vec3 p, vec2 h )
      {
          const vec3 k = vec3(-0.8660254, 0.5, 0.57735);
          p = abs(p);
          p.xy -= 2.0*min(dot(k.xy, p.xy), 0.0)*k.xy;
          vec2 d = vec2(
             length(p.xy-vec2(clamp(p.x,-k.z*h.x,k.z*h.x), h.x))*sign(p.y-h.x),
             p.z-h.y );
          return min(max(d.x,d.y),0.0) + length(max(d,0.0));
      }

      float distFunc(vec3 p){
        float size = 1.0;
      //  p = rotate(p, radians(time * 10.0), vec3(1.0, 0.5, 0.0));
        float d1 = sdSphere(p,size);
        float d2 = sdHexPrism(p, vec2(size * 0.2,1.0));
        float d3 = sdHexPrism(rotate(p,radians(23.0),vec3(0.5,0.9,0.0)), vec2(size * 0.2,1.0));
        float d4 = sdHexPrism(rotate(p,radians(40.0),vec3(1.0,0.4,0.0)), vec2(size * 0.2,1.0));


        return max(d1,max(-d2,max(-d3,-d4)));
      }


      vec3 genNormal(vec3 p){
          float d = 0.0001;
          return normalize(vec3(
              distFunc(p + vec3(  d, 0.0, 0.0)) - distFunc(p + vec3( -d, 0.0, 0.0)),
              distFunc(p + vec3(0.0,   d, 0.0)) - distFunc(p + vec3(0.0,  -d, 0.0)),
              distFunc(p + vec3(0.0, 0.0,   d)) - distFunc(p + vec3(0.0, 0.0,  -d))
          ));
      }

      float genShadow(vec3 ro, vec3 rd){
          float h = 0.0;
          float c = 0.001;
          float r = 1.0;
          float shadowCoef = 0.5;
          for(float t = 0.0; t < 50.0; t++){
              h = distFunc(ro + rd * c);
              if(h < 0.001){
                  return shadowCoef;
              }
              r = min(r, h * 16.0 / c);
              c += h;
          }
          return 1.0 - shadowCoef + r * shadowCoef;
      }


      void main(void){
      //    lightDir.x *= sin(time * 2.0);
      //    lightDir.y *= cos(time * 1.5);

          // fragment position
          vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);

          // camera and ray
          vec3 cSide = cross(cDir, cUp);
          float targetDepth = 1.0;
          vec3 ray = normalize(cSide * p.x + cUp * p.y + cDir * targetDepth);

          // marching loop
          float tmp, dist;
          tmp = 0.0;
          vec3 dPos = cPos;
          for(int i = 0; i < 256; i++){
              dist = distFunc(dPos);
              if(dist < 0.001){break;}
              tmp += dist;
              dPos = cPos + tmp * ray;
          }

          // light offset
          vec3 light = normalize(lightDir + vec3(sin(time), 0.0, 0.0));

          // hit check
          vec3 color;
          float shadow = 1.0;
          if(abs(dist) < 0.001){
              // generate normal
              vec3 normal = genNormal(dPos);

              // light
              vec3 halfLE = normalize(light - ray);
              float diff = clamp(dot(light, normal), 0.1, 1.0);
              float spec = pow(clamp(dot(halfLE, normal), 0.0, 1.0), 50.0);

              // generate shadow
              shadow = genShadow(dPos + normal * 0.001, light);

              // generate tile pattern
            /*  float u = 1.0 - floor(mod(dPos.x, 2.0));
              float v = 1.0 - floor(mod(dPos.z, 2.0));
              if((u == 1.0 && v < 1.0) || (u < 1.0 && v == 1.0)){
                  diff *= 0.7;
              }*/

              color = vec3(1.0, 1.0, 1.0) * diff + vec3(spec);
          }else{
              color = vec3(0.0);
          }
          gl_FragColor = vec4(color * max(0.5, shadow), 1.0);
      }

    </script>


    <script id="bvs" type="x-shader/x-vertex">
      attribute vec3 position;

      void main(void){
        gl_Position = vec4(position, 1.0);
      }
    </script>

    <script id="bfs" type="x-shader/x-fragment">
      precision mediump float;

      uniform float time;
      uniform vec2  mouse;
      uniform vec2  resolution;


      const int   oct  = 8;
      const float per  = 0.5;
      const float PI   = 3.1415926;
      const float cCorners = 1.0 / 16.0;
      const float cSides   = 1.0 / 8.0;
      const float cCenter  = 1.0 / 4.0;

      // 補間関数
      float interpolate(float a, float b, float x){
          float f = (1.0 - cos(x * PI)) * 0.5;
          return a * (1.0 - f) + b * f;
      }

      // 乱数生成
      float rnd(vec2 p){
          return fract(sin(dot(p ,vec2(12.9898,78.233))) * 43758.5453);
      }

      // 補間乱数
      float irnd(vec2 p){
          vec2 i = floor(p);
          vec2 f = fract(p);
          vec4 v = vec4(rnd(vec2(i.x,       i.y      )),
                        rnd(vec2(i.x + 1.0, i.y      )),
                        rnd(vec2(i.x,       i.y + 1.0)),
                        rnd(vec2(i.x + 1.0, i.y + 1.0)));
          return interpolate(interpolate(v.x, v.y, f.x), interpolate(v.z, v.w, f.x), f.y);
      }

      // ノイズ生成
      float noise(vec2 p){
          float t = 0.0;
          for(int i = 0; i < oct; i++){
              float freq = pow(2.0, float(i));
              float amp  = pow(per, float(oct - i));
              t += irnd(vec2(p.x / freq, p.y / freq)) * amp;
          }
          return t;
      }

      // シームレスノイズ生成
      float snoise(vec2 p, vec2 q, vec2 r){
          return noise(vec2(p.x,       p.y      )) *        q.x  *        q.y  +
                 noise(vec2(p.x,       p.y + r.y)) *        q.x  * (1.0 - q.y) +
                 noise(vec2(p.x + r.x, p.y      )) * (1.0 - q.x) *        q.y  +
                 noise(vec2(p.x + r.x, p.y + r.y)) * (1.0 - q.x) * (1.0 - q.y);
      }

      void main(void){
          // noise
          vec2 t = gl_FragCoord.xy + vec2(time * 10.0);
          float n = noise(t);

          // seamless noise
      //	const float map = 256.0;
      //	vec2 t = mod(gl_FragCoord.xy + vec2(time * 10.0), map);
      //	float n = snoise(t, t / map, vec2(map));

          gl_FragColor = vec4(vec3(n)*vec3(0.1,0.1,0.4), 1.0);
      }

    </script>

  </head>
  <body>

    <div class="canvas-wrapper">
      <canvas id="background"></canvas>
      <canvas id="canvas"></canvas>
    </div>

    <div class= "header">

      <!--<img class="titleimg" src="img/title.png">-->
      <!--<h1 class="title">portfolio</h1>-->

      <p><input type="checkbox" id="check" checked><label for="check"> auto run</label></p>

    </div>


  </body>
</html>
